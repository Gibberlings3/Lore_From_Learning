// why isn't this an ALWAYS block? It should be, but it messes with the secret translator component so it's just included everywhere instead

ACTION_IF ((FILE_EXISTS ~dlc/sod-dlc.zip~) OR (FILE_EXISTS ~sod-dlc.zip~)) THEN BEGIN FAIL @1 END // Modmerge check

ACTION_IF GAME_IS ~iwd_in_bg2~ THEN BEGIN WARN @3 END 

ACTION_IF GAME_IS ~bgee bg2ee iwdee eet pstee~ BEGIN // ee games

  OUTER_SET enhanced_edition = 1 // re-using David's variables
  OUTER_SPRINT ~tra_location~ ~weidu_external/cdlore/languages~

  // convert strings to UTF-8 for BGEE/BG2EE
  ACTION_DEFINE_ARRAY cdreload BEGIN game END
  ACTION_DEFINE_ARRAY cdnoconvert BEGIN weidu END // List of tra files that contain ONLY strings for the WeiDU installer and NOT game content
  LAF HANDLE_CHARSETS INT_VAR infer_charsets = 1 STR_VAR tra_path = ~cdlore/languages~ out_path = ~weidu_external/cdlore/languages~ noconvert_array = cdnoconvert reload_array = cdreload END
  
END ELSE BEGIN

  OUTER_SPRINT ~tra_location~ ~cdlore/languages~
  
END

// these should get set by customize.tpa, but we include defaults in case they delete important bits from that 
OUTER_SET actions_per_rest = 3
OUTER_SET books_to_read       =  5 // number of books to read until you gain a point of lore
OUTER_SET max_lore_from_books = 10 // the amount of lore, total, that you can earn from books over the course of the game
OUTER_SET number_weapon_profs = 6 // number of different weapons you need a star in to gain a point of lore
OUTER_SET count_weapon_styles = 0 // set this to 1 if you want the weapon styles to count for the different weapons learned, otherwise 0
OUTER_SET other_races  =  3 // number of races (NOT counting PC) that must be in the party to qualify for bonus
OUTER_SET race_bonus_1 =  3 // number of rests until you get the first bonus
OUTER_SET race_bonus_2 = 10 // number of rests until you get the second bonus
OUTER_SET race_bonus_3 = 30 // number of rests until you get the final bonus
OUTER_SET other_classes =  4 // number of classes (NOT counting PC) that must be in the party to qualify for bonus
OUTER_SET class_bonus_1 =  5 // number of rests until you get the first bonus
OUTER_SET class_bonus_2 = 15 // number of rests until you get the second bonus
OUTER_SET class_bonus_3 = 45 // number of rests until you get the final bonus
OUTER_SET monster_bonus_1 = 10 // number of monster races that need to be killed to get the first bonus
OUTER_SET monster_bonus_2 = 30 // number of monster races that need to be killed to get the second bonus
OUTER_SET monster_bonus_3 = 60 // number of monster races that need to be killed to get the final bonus

INCLUDE ~cdlore/customize.tpa~

// some derived vars for the scripting
OUTER_SET books_to_read_min = books_to_read - 1
OUTER_SET max_lore_from_books_bcs = max_lore_from_books + 1
OUTER_SET martial_min = number_weapon_profs - 1
// apparently R_B_B doesn't resolve @, so we convert these to vars
OUTER_SET cdlore_string_2001 = RESOLVE_STR_REF (@2001)
OUTER_SET cdlore_string_2002 = RESOLVE_STR_REF (@2002)
OUTER_SET cdlore_string_3001 = RESOLVE_STR_REF (@3001)
OUTER_SET cdlore_string_3002 = RESOLVE_STR_REF (@3002)
OUTER_SET cdlore_string_3003 = RESOLVE_STR_REF (@3003)
OUTER_SET cdlore_string_3004 = RESOLVE_STR_REF (@3004)
OUTER_SET cdlore_string_3005 = RESOLVE_STR_REF (@3005)
OUTER_SET cdlore_string_3006 = RESOLVE_STR_REF (@3006)
OUTER_SET cdlore_string_4001 = RESOLVE_STR_REF (@4001)
OUTER_SET cdlore_string_4002 = RESOLVE_STR_REF (@4002)
OUTER_SET cdlore_string_4003 = RESOLVE_STR_REF (@4003)

OUTER_SPRINT "tutu_var" ""  
ACTION_IF GAME_IS ~tutu tutu_totsc~ THEN BEGIN OUTER_SPRINT "tutu_var" "_" END
  
COPY ~cdlore/spl/cdlore1.spl~ ~override~
  SAY 0x9e @100 // "You have earned one point of lore."

// OK, so why do we do this goofy wrapup sequence? Given the install order the actual lore point from
// books could preempt the collection of points for the race/class counters. So anything that does
// not end with Continue() is shifted back to the bottom with every component. A little more coding
// but makes it seamless for the players and avoids a standalone wrapup component. 
DEFINE_ACTION_FUNCTION cd_lore_wrapup  
  INT_VAR 
    cd_wrapup_books = 0
    cd_wrapup_martial = 0
    cd_wrapup_party = 0
    cd_wrapup_monster = 0
BEGIN   

  // first delete the old end blocks
  COPY_EXISTING ~player1d.bcs~ ~override~
    PATCH_IF MOD_IS_INSTALLED ~cdlore/cdlore.tp2~ ~10~ BEGIN
      REPLACE_BCS_BLOCK EVALUATE_BUFFER ~cdlore/baf/book_bottom.baf~    ~cdlore/baf/blank.baf~
    END  
    PATCH_IF MOD_IS_INSTALLED ~cdlore/cdlore.tp2~ ~20~ BEGIN
      REPLACE_BCS_BLOCK EVALUATE_BUFFER ~cdlore/baf/martial_bottom.baf~ ~cdlore/baf/blank.baf~
    END  
    PATCH_IF MOD_IS_INSTALLED ~cdlore/cdlore.tp2~ ~30~ BEGIN
      REPLACE_BCS_BLOCK EVALUATE_BUFFER ~cdlore/baf/party_bottom.baf~   ~cdlore/baf/blank.baf~
    END  
    PATCH_IF MOD_IS_INSTALLED ~cdlore/cdlore.tp2~ ~40~ BEGIN
      REPLACE_BCS_BLOCK EVALUATE_BUFFER ~cdlore/baf/monster_bottom.baf~ ~cdlore/baf/blank.baf~
    END  
    // now add back as needed in preferred order
    DECOMPILE_AND_PATCH BEGIN
      PATCH_IF ((MOD_IS_INSTALLED ~cdlore/cdlore.tp2~ ~30~) OR (cd_wrapup_party = 1)) BEGIN
        APPEND_FILE ~cdlore/baf/party_bottom.baf~ EVALUATE_BUFFER
      END  
      PATCH_IF ((MOD_IS_INSTALLED ~cdlore/cdlore.tp2~ ~20~) OR (cd_wrapup_martial = 1)) BEGIN
        APPEND_FILE ~cdlore/baf/martial_bottom.baf~ EVALUATE_BUFFER
      END  
      PATCH_IF ((MOD_IS_INSTALLED ~cdlore/cdlore.tp2~ ~40~) OR (cd_wrapup_monster = 1)) BEGIN
        APPEND_FILE ~cdlore/baf/monster_bottom.baf~ EVALUATE_BUFFER
      END  
      PATCH_IF ((MOD_IS_INSTALLED ~cdlore/cdlore.tp2~ ~10~) OR (cd_wrapup_books = 1)) BEGIN
        APPEND_FILE ~cdlore/baf/book_bottom.baf~ EVALUATE_BUFFER
      END  
    END  
    BUT_ONLY

END